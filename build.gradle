buildscript {
    ext {
        springBootVersion = '2.1.6.RELEASE'
        javaVersion = '1.8'

    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

group 'com.trabricks.boot'
version '1.0.0'

subprojects {
    repositories {
        mavenCentral()

        maven {
            url = 'https://mvn.creamcookie.cc/content/repositories/m2/'
        }
    }

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'

    group 'com.trabricks.boot'

    targetCompatibility = javaVersion
    sourceCompatibility = javaVersion

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }

        dependencies {
            dependency 'org.modelmapper:modelmapper:2.3.5'
            dependency 'com.google.guava:guava:27.0.1-jre'
            dependency 'org.apache.commons:commons-lang3:3.8.1'
            dependency 'jmimemagic:jmimemagic:0.1.2'
            dependency 'org.hibernate:hibernate-validator:6.0.16.Final'
            dependency 'com.github.darrachequesne:spring-data-jpa-datatables:5.0.0'
        }
    }

    dependencies {
        compile 'com.trabricks.boot:trabricks-spring-boot-starter-commons:1.0.1'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompile 'org.projectlombok:lombok'
        testCompile 'junit:junit:4.12'
        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.java)
            }
        }
        repositories {
            maven {
                url = 'https://mvn.creamcookie.cc/content/repositories/m2/'
                credentials {
                    username getProperty('nexusUser')
                    password getProperty('nexusPassword')
                }
            }
        }
    }
}